apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'hugo'

// keystorePropertiesFile 변수를 만들고 프로젝트 폴더의 keystore.properties 파일로 초기화 한다.
def keystorePropertiesFile = rootProject.file("keystore.properties")
// keystoreProperties 변수를 만들고 Properties() 객체를 초기화 한다.
def keystoreProperties = new Properties()
// keystore.properties 파일을 keystoreProperties 객체에 로드한다.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
  compileSdkVersion 26
  buildToolsVersion "26.0.0"
  def versionMajor = 0
  def versionMinor = 1
  def versionCodeNumber = 1
  defaultConfig {
    applicationId "io.github.ovso.righttoknow"
    minSdkVersion 16
    targetSdkVersion 26
    versionCode versionCodeNumber
    versionName "${versionMajor}.${versionMinor}.${versionCodeNumber}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  lintOptions {
    abortOnError false
  }
  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      storePassword keystoreProperties['storePassword']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-rules.pro'
      proguardFile 'proguard-tedpermission.pro'
      proguardFile 'proguard-firebase.pro'
      signingConfig signingConfigs.release
      applicationVariants.all { variant ->
        variant.outputs.each { output ->
          def formattedDate = new Date().format('yyyyMMdd_HHmmss')
          def apkName = "RightToKnow_" + variant.versionName + "_" + formattedDate + "_release.apk"
          output.outputFile = new File(output.outputFile.parent, apkName)
        }
      }
    }
    debug {
      versionNameSuffix '-debug'
    }
  }
  configurations.all {
    resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  compile 'com.android.support:appcompat-v7:26.+'
  compile 'com.android.support:design:26.+'
  compile 'com.android.support:cardview-v7:26.+'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  testCompile 'junit:junit:4.12'

  compile 'com.jakewharton:butterknife:8.7.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
  provided 'org.projectlombok:lombok:1.16.18'
  compile 'gun0912.ted:tedpermission:1.0.3'
  compile 'com.google.android.gms:play-services-location:11.0.4'
  compile 'com.google.firebase:firebase-database:11.0.4'
  compile 'com.google.firebase:firebase-storage:11.0.4'
  compile 'com.github.barteksc:android-pdf-viewer:2.6.1'
  compile 'de.psdev.licensesdialog:licensesdialog:1.8.3'
}

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'